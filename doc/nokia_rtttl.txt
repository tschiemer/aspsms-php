;
; Ringing Tones text transfer language (RTTTL)
;
; Version 1.0 / 29-July-1998
; - First version by John Mostelo
; 
; http://members.tripod.com/~ringtones/note_syntax.txt
;
<ringing-tones-text-transfer-language> :=
<name> <sep> [<defaults>] <sep> <note-command>+

<name> := <char>+ ; maximum name length 10 characters

<sep> := ":"

<defaults> := 
<def-note-duration> |
<def-note-scale> |
<def-beats>

<def-note-duration> := "d=" <duration>

<def-note-scale> := "o=" <scale>

<def-beats> := "b=" <beats-per-minute>

<beats-per-minute> := 25,28,...,900 ; decimal value

; If not specified, defaults are
;
; 4 = duration
; 6 = scale 
; 63 = beats-per-minute


<note-command> :=
[<duration>] <note> [<scale>] [<special-duration>] <delimiter>


<duration> :=
"1" | ; Full 1/1 note
"2" | ; 1/2 note
"4" | ; 1/4 note
"8" | ; 1/8 note
"16" | ; 1/16 note
"32" | ; 1/32 note

<note> :=
"P" | ; pause
"C" |
"C#" |
"D" |
"D#" |
"E" |
"F" |
"F#" |
"G" |
"G#" |
"A" |
"A#" |
"H"

<scale> :=
"5" | ; Note A is 440Hz
"6" | ; Note A is 880Hz
"7" | ; Note A is 1.76 kHz
"8" ; Note A is 3.52 kHz

<special-duration> :=
"." ; Dotted note

<delimiter> := ","

; End of specification




RTTTL Format Specifications

RTTTL (RingTone Text Transfer Language) is the primary format used to distribute 
ringtones for Nokia phones. An RTTTL file is a text file, containing the 
ringtone name, a control section and a section containing a comma separated 
sequence of ring tone commands. White space must be ignored by any reader 
application. 

Example: 
Simpsons:d=4,o=5,b=160:32p,c.6,e6,f#6,8a6,g.6,e6,c6,8a,8f#,8f#,8f#,2g

This file describes a ringtone whose name is 'Simpsons'. The control section 
sets the beats per minute at 160, the default note length as 4, and the default 
scale as Octave 5. 
<RTX file> := <name> ":" [<control section>] ":" <tone-commands>

	<name> := <char> ; maximum name length 10 characters

	<control-section> := <control-pair> ["," <control-section>]

		<control-pair> := <control-name> ["="] <control-value>

		<control-name> := "o" | "d" | "b"
		; Valid in control section: o=default scale, d=default duration, b=default beats per minute. 
		; if not specified, defaults are 4=duration, 6=scale, 63=beats-per-minute
		; any unknown control-names must be ignored

		<tone-commands> := <tone-command> ["," <tone-commands>]

		<tone-command> :=<note> | <control-pair>

		<note> := [<duration>] <note> [<scale>] [<special-duration>] <delimiter>

			<duration> := "1" | "2" | "4" | "8" | "16" | "32" 
			; duration is divider of full note duration, eg. 4 represents a quarter note

			<note> := "P" | "C" | "C#" | "D" | "D#" | "E" | "F" | "F#" | "G" | "G#" | "A" | "A#" | "B" 

			<scale> :="4" | "5" | "6" | "7"
			; Note that octave 4: A=440Hz, 5: A=880Hz, 6: A=1.76 kHz, 7: A=3.52 kHz
			; The lowest note on the Nokia 61xx is A4, the highest is B7

			<special-duration> := "." ; Dotted note

; End of specification


